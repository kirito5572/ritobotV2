import java.text.SimpleDateFormat

plugins {
    id("java")
    id("application")
    id'com.github.johnrengelman.shadow' version '7.1.2'
}

mainClassName = "com.kirito5572.App"
group = "com.kirito5572"
version = "2.0-beta2"

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    maven {
        name 'duncte123-jfrog'
        url 'https://duncte123.jfrog.io/artifactory/maven'
    }
    maven {
        name 'm2-duncte123'
        url 'https://m2.duncte123.dev/releases'
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'm2-chew'
        url "https://m2.chew.pro/releases"
    }
}

shadowJar {
    zip64 true
    manifest {
        attributes(
                'Title': 'ritobotV2',
                'Main-Class': mainClassName,
                'Group': group,
                'Version': archiveVersion,
                'BuiltBy'       : "kirito5572",
                'BuildDate': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'CreatedBy'     : "Gradle ${gradle.gradleVersion}",
                'BuildJDK'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'BuildOS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

dependencies {
    //https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.44.1.0'
    //https://central.sonatype.com/artifact/com.github.oshi/oshi-core?smo=true
    implementation group: 'com.github.oshi', name: 'oshi-core', version: '6.4.8'
    //https://github.com/discord-jda/JDA
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-beta.18'
    //https://search.maven.org/artifact/ch.qos.logback/logback-classic
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.14'
    //https://search.maven.org/artifact/org.fusesource.jansi/jansi
    //DO NOT UPDATE, if update jansi error in logback occur
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '1.17.1'
    //https://jsoup.org/download
    implementation group: 'org.jsoup', name:'jsoup', version: '1.17.1'
    //legacy
    //https://github.com/sedmelluq/lavaplayer
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.77'
    //https://central.sonatype.com/artifact/com.mysql/mysql-connector-j
    implementation group: 'com.mysql', name: 'mysql-connector-j', version: '8.2.0'
    //https://m2.duncte123.dev/#/releases/me/duncte123/botCommons
    implementation group: 'me.duncte123', name: 'botCommons', version: '3.0.16'
    //https://github.com/google/gson/releases
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    //https://central.sonatype.com/artifact/org.apache.groovy/groovy-all?smo=true
    implementation group: 'org.apache.groovy', name: 'groovy-all', version: '4.0.16'
    //https://search.maven.org/artifact/javax.xml.bind/jaxb-api
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
    //https://central.sonatype.com/artifact/com.amazonaws/aws-java-sdk-bom?smo=true
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.603')
    implementation 'com.amazonaws:aws-java-sdk-s3'
    implementation platform('com.google.cloud:libraries-bom:26.29.0')
    implementation 'com.google.cloud:google-cloud-translate'
    //https://search.maven.org/artifact/commons-validator/commons-validator
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.8.0'
}

tasks.test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '8.5'
}

compileJava.options.encoding = 'UTF-8'