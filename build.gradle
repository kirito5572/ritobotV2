import java.text.SimpleDateFormat

plugins {
    id'java'
    id'application'
    id'com.gradleup.shadow' version '9.2.2'
}


application {
    mainClass = "me.kirito5572.App"
}
group = "me.kirito5572"
version = "2.0.4-beta"
compileJava.options.encoding = 'UTF-8'

repositories ({
    mavenCentral()
    google()
    maven {
        url = "https://plugins.gradle.org/m2/"
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        name = 'm2-duncte123'
        url = 'https://m2.duncte123.dev/releases'
    }
    maven {
        name = 'm2-dv8tion'
        url = 'https://m2.dv8tion.net/releases'
    }
    maven {
        name = 'm2-chew'
        url = "https://m2.chew.pro/releases"
    }
})

shadowJar{
    zip64 = true
    manifest {
        attributes(
                'Title': 'ritobotV2',
                'Main-Class': application.mainClass.get(),
                'Group': group,
                'Version': archiveVersion,
                'BuiltBy'       : "kirito5572",
                'BuildDate': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'CreatedBy'     : "Gradle ${gradle.gradleVersion}",
                'BuildJDK'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'BuildOS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}


dependencies {
    //https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation 'org.xerial:sqlite-jdbc:3.51.0.0'
    //https://central.sonatype.com/artifact/com.github.oshi/oshi-core?smo=true
    implementation 'com.github.oshi:oshi-core:6.9.1'
    //https://github.com/discord-jda/JDA
    implementation 'net.dv8tion:JDA:6.1.2'
    //https://search.maven.org/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.5.21'
    //https://search.maven.org/artifact/org.fusesource.jansi/jansi
    //DO NOT UPDATE, if update jansi error in logback occur
    implementation 'org.fusesource.jansi:jansi:1.17.1'
    //https://jsoup.org/download
    implementation 'org.jsoup:jsoup:1.21.2'
    //legacy
    //https://github.com/sedmelluq/lavaplayer
    implementation 'com.sedmelluq:lavaplayer:1.3.78'
    //https://central.sonatype.com/artifact/com.mysql/mysql-connector-j
    implementation 'com.mysql:mysql-connector-j:9.5.0'
    //https://m2.duncte123.dev/#/releases/me/duncte123/botCommons
    implementation 'me.duncte123:botCommons:3.1.18'
    //https://github.com/google/gson/releases
    implementation 'com.google.code.gson:gson:2.13.2'
    //https://central.sonatype.com/artifact/org.apache.groovy/groovy-all?smo=true
    implementation 'org.apache.groovy:groovy-all:5.0.2'
    //https://central.sonatype.com/artifact/javax.xml.bind/jaxb-api
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.21.0'
    //https://central.sonatype.com/artifact/software.amazon.awssdk/bom
    implementation(platform("software.amazon.awssdk:bom:2.39.3"))
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sso'
    //https://mvnrepository.com/artifact/com.google.cloud/libraries-bom
    implementation platform('com.google.cloud:libraries-bom:26.72.0')
    implementation 'com.google.cloud:google-cloud-translate'
    //https://search.maven.org/artifact/commons-validator/commons-validator
    implementation 'commons-validator:commons-validator:1.10.1'
}

tasks.test {
    useJUnitPlatform()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
